plugins {
    id 'spotifybackup.java-application-conventions'
    id 'application'
    id 'java'
    id 'extra-java-module-info'
}

dependencies {
    implementation(
            'org.apache.commons:commons-text:1.10.0',
            'commons-codec:commons-codec:1.16.0',
            'org.hibernate.orm:hibernate-core:6.2.7.Final',
            'org.hibernate.orm:hibernate-jpamodelgen:6.2.7.Final',
            'org.hibernate.orm:hibernate-hikaricp:6.2.7.Final',
            'org.projectlombok:lombok:1.18.28',
            'com.h2database:h2:2.2.220',
            'org.slf4j:slf4j-nop:2.0.7',
            'se.michaelthelin.spotify:spotify-web-api-java:8.0.0',
            'com.neovisionaries:nv-i18n:1.29',
            'io.hypersistence:hypersistence-utils-hibernate-62:3.5.0'
    )
    annotationProcessor 'org.projectlombok:lombok:1.18.28'
}

extraJavaModuleInfo {
    module("nv-i18n-1.29.jar", "nv.i18n", "1.29") {
        exports("com.neovisionaries.i18n")
    }
}

test {
    useJUnitPlatform()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(20)
    }
}

application {
    // Define the main class for the application.
    mainClass = 'spotifybackup.app.App'
    applicationDefaultJvmArgs = ['-Dgreeting.language=en']
}

jar {
    manifest {
        attributes 'Main-Class': application.mainClass
    }
    archiveBaseName = rootProject.name
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}
